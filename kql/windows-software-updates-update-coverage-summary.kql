//Shows all updates newer than 30 days and all updates with < 100% coverage and renders their coverage summary
Update
| where MSRCSeverity in ("Critical", "Important", "Moderate", "Security")
| where PublishedDate > now(-30days) or UpdateID in ( 
    (Update
    | where MSRCSeverity in ("Critical", "Important", "Moderate", "Security")
    | summarize arg_max(TimeGenerated, *) by UpdateID,SourceComputerId
    | where UpdateState != "Installed"
    | summarize count() by UpdateID
    | project UpdateID)
)
| summarize arg_max(TimeGenerated, *) by UpdateID,SourceComputerId
| summarize NotInstalledCount = countif(UpdateState != "Installed"), InstalledCount = countif(UpdateState == "Installed"), TotalCount = count() by Product, MSRCSeverity, PublishedDate, KBID, Title
| project Update=strcat(Product," - ", Title), MSRCSeverity, DaysOld=datetime_diff("Day",now(),PublishedDate), NotInstalledCount,InstalledCount,TotalCount